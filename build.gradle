buildscript {
    repositories {
        jcenter() // this applies only to the Gradle 'Shadow' plugin
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:5.2.0'
    }
}

plugins {
    id 'java'
    id 'application'
    // shadow plugin to produce fat JARs
    id 'com.github.johnrengelman.shadow' version '5.2.0'

    // required(flink-influxdb): added
    id "io.freefair.lombok" version "5.3.0"
}


// artifact properties
group = 'de.hpi.des.influxdb'
version = '0.1-SNAPSHOT'
mainClassName = 'de.hpi.des.influxdb.PlaneJob'
description = """Flink Connector InfluxDB Example"""

ext {
    javaVersion = '1.8'
    flinkVersion = '1.12.1'
    scalaBinaryVersion = '2.11'
    slf4jVersion = '1.7.15'
    log4jVersion = '2.12.1'
}


sourceCompatibility = javaVersion
targetCompatibility = javaVersion
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

// declare where to find the dependencies of your project
repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.wso2.org/nexus/content/repositories/releases' }
    maven { url 'http://dist.wso2.org/maven2/' }
}

// NOTE: We cannot use "compileOnly" or "shadow" configurations since then we could not run code
// in the IDE or with "gradle run". We also cannot exclude transitive dependencies from the
// shadowJar yet (see https://github.com/johnrengelman/shadow/issues/159).
// -> Explicitly define the // libraries we want to be included in the "flinkShadowJar" configuration!
configurations {
    compile
    flinkShadowJar // dependencies which go into the shadowJar

    // always exclude these (also from transitive dependencies) since they are provided by Flink
    flinkShadowJar.exclude group: 'org.apache.flink', module: 'force-shading'
    flinkShadowJar.exclude group: 'com.google.code.findbugs', module: 'jsr305'
    flinkShadowJar.exclude group: 'org.slf4j'
    flinkShadowJar.exclude group: 'log4j'

    compile.exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
}

// declare the dependencies for your production and test code
dependencies {
    // --------------------------------------------------------------
    // Compile-time dependencies that should NOT be part of the
    // shadow jar and are provided in the lib folder of Flink
    // --------------------------------------------------------------
    compile "org.apache.flink:flink-java:1.12.1"
    compile "org.apache.flink:flink-streaming-java_2.11:1.12.1"
    compile "org.apache.flink:flink-clients_2.11:1.12.1"

    // --------------------------------------------------------------
    // Dependencies that should be part of the shadow jar, e.g.
    // connectors. These must be in the flinkShadowJar configuration!
    // --------------------------------------------------------------

    compile "org.apache.flink:flink-table-planner-blink_2.11:1.12.1"
    compile 'org.apache.flink:flink-table-api-java-bridge_2.11:1.12.1'
    compile 'com.github.1p4pk:bahir-flink:521df6c23f0a2a788d0853a260d31caaca33f31b'
    flinkShadowJar "org.apache.flink:flink-table-planner-blink_2.11:1.12.1"
    flinkShadowJar 'com.github.1p4pk:bahir-flink:521df6c23f0a2a788d0853a260d31caaca33f31b'


    // Add test dependencies here.
    // testCompile "junit:junit:4.12"
}

// make compileOnly dependencies available for tests:
sourceSets {
    main.compileClasspath += configurations.flinkShadowJar
    main.runtimeClasspath += configurations.flinkShadowJar

    test.compileClasspath += configurations.flinkShadowJar
    test.runtimeClasspath += configurations.flinkShadowJar

    javadoc.classpath += configurations.flinkShadowJar
}

run.classpath = sourceSets.main.runtimeClasspath

jar {
    manifest {
        attributes 'Built-By': System.getProperty('user.name'),
                'Build-Jdk': System.getProperty('java.version')
    }
}

shadowJar {
    configurations = [project.configurations.flinkShadowJar]
    zip64 true
    minimize()
}
